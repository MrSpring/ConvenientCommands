/*
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
*/

// for people who want stable
plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.1"
}

apply plugin: 'maven'

String mod_version = "1.0.0"
String mc_version = "1.8"
version = "${mod_version}-${mc_version}"
group= "dk.mrspring" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ConvenientCommands"
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

minecraft {
    version = "1.8-11.14.3.1519"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20141130"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

task devJar(type: Jar) {
    from (sourceSets.main.output, sourceSets.api.output)
    classifier = "deobf"
}

sourceJar {
    classifier = "src"
}

artifacts {
    archives sourceJar, devJar
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'mod_version':mod_version/*project.version*/, 'mc_version':mc_version/*project.minecraft.version*/
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

uploadArchives {
    dependsOn "build"
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "${config.maven_url}") {
                authentication(userName: "${config.maven_user}", password: "${config.maven_pass}")
            }
        }
    }
}
